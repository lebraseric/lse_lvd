1*      ******          LA GUERRE ATOMIQUE         ******
2*      ***            AUTEUR: JACQUES EESES          ***
3*      * COPYRIGHT L'ORDINATEUR INDIVIDUEL ET L'AUTEUR *
4* ADAPTE EN LSE PAR ERIC LE BRAS  4.2
8 AFFICHER[25/]
10 TABLEAU T[11,11],V[10,10];P_20;E_0;S_0
11 CHAINE LA,LZ,JU
12 AFFICHER 'QUELQUES INSTANTS DE PATIENCE, JE PLACE MES SILOS'
14*                                        INITIALISATION
15 &INI()
16 FAIRE 20 POUR J_1 JUSQUA P
18 C_ENT(ALE(0)*10)+1;L_ENT(ALE(0)*10)+1
20 T[L,C]_T[L,C]+1
24 FAIRE 30 POUR J_1 JUSQUA 10
26 FAIRE 28 POUR K_1 JUSQUA 10
28 V[J,K]_EQN('.')
30*
32 AFFICHER[25/];ALLER EN 44
34*                                LE JOUEUR JOUE
36 AFFICHER 'OU JOUEZ-VOUS ? ';LIRE LA,C
38 L_EQN(LA)-64
40 SI C<1 OU C>10 OU L<1 OU L>10 ALORS ALLER EN 34
42 V[L,C]_EQN('X');E_E+1
44*                        AFFICHAGE DE LA GRILLE
46 AFFICHER EQC(28)
48 AFFICHER '   1   2   3   4   5   6   7   8   9   10'
50 FAIRE 62 POUR J_1 JUSQUA 10
52 LZ_EQC(J+64)!'  '
54 FAIRE 58 POUR K_1 JUSQUA 10
56 LZ_LZ!EQC(V[J,K])!'   '
58*
60 AFFICHER LZ
62*
64*                         AFFICHAGE DES RESULTATS
66 SI E=0 ALORS ALLER EN 34
68 LZ_'COUP NUMERO '!CCA(E)!'            '
70 SI T[L,C]=0 ALORS DEBUT &REP1();ALLER EN 82 FIN
72 LZ_'VOUS AVEZ DETRUIT '!CCA(T[L,C])!' SILO'
74 SI T[L,C]>1 ALORS LZ_LZ!'S'
76 LZ_LZ!' EN '!LA!','!CCA(C);AFFICHER LZ
78 P_P-T[L,C];T[L,C]_0
80 SI P=0 ALORS ALLER EN 126
82 AFFICHER 'NOMBRE DE SILOS RESTANT A DETRUIRE : ',P
84 FAIRE 90 POUR J_1 JUSQUA L-1
86 FAIRE 88 POUR K_1 JUSQUA 10
88 S_S+T[J,K]
90*
92 LZ_'NOMBRE DE SILOS AU NORD  :'!CCA(S)!'  ';S_0
94 FAIRE 100 POUR J_L+1 JUSQUA 10
96 FAIRE 98 POUR K_1 JUSQUA 10
98 S_S+T[J,K]
100*
102 LZ_LZ!'NOMBRE DE SILOS AU SUD   :'!CCA(S)!'  ';AFFICHER LZ;S_0
104 FAIRE 110 POUR J_1 JUSQUA 10
106 FAIRE 108 POUR K_1 JUSQUA C-1
108 S_S+T[J,K]
110*
112 LZ_'NOMBRE DE SILOS A L"OUEST:'!CCA(S)!'  ';S_0
114 FAIRE 120 POUR J_1 JUSQUA 10
116 FAIRE 118 POUR K_C+1 JUSQUA 10
118 S_S+T[J,K]
120*
122 LZ_LZ!'NOMBRE DE SILOS A L"EST  :'!CCA(S)!'  ';AFFICHER LZ;S_0
124 ALLER EN 34
126 AFFICHER 'BRAVO VOUS AVEZ DETRUIT TOUS LES SILOS'
128 AFFICHER '       EN ',E,' COUPS !'
130*                               UNE AUTRE PARTIE ?
132 AFFICHER 'VOULEZ-VOUS JOUER UNE AUTRE PARTIE ? ';LIRE JU
134 SI JU='OUI' ALORS ALLER EN 8
136 AFFICHER[12X,'AU REVOIR']
138 TERMINER
200 PROCEDURE &INI()
202 FAIRE 210 POUR I_1 JUSQUA 10
204 FAIRE 208 POUR K_1 JUSQUA 10
208 T[I,K]_0
210*
211 RETOUR
215 PROCEDURE &REP1()
217 LZ_LZ!'RIEN EN '!LA!','!CCA(C);AFFICHER LZ;RETOUR
