1* PROGRAMME DE RECHERCHE DE PRONOMS DANS UN TEXTE
2* ERIC LE BRAS  4.2                               N.218
3 AFFICHER[25/,25X,'RECHERCHE DE PRONOMS DANS UN TEXTE',/,25X,34'-',10/]
4 CHAINE NOM,TEX,TEXT,PL,NBPRO,TITRE,OTEUR,CAT,PROCA,CATE,QUIZ
5 NEN_0;NC_0;ZPRO_0
6 TABLEAU P[7]
7 AFFICHER 'NOM DU FICHIER TEXTE : ';LIRE NOM
10 &AFTEX()
11 AFFICHER 'COMPTE LES PRONOMS ET DIS-MOI COMBIEN TU EN AS TROUVE: ';LIRE NPRO
12 &NBPRO();AFFICHER[/];SI NPRO=ZPRO ALORS ALLER EN 17
13 SI NPRO>ZPRO-5 ET NPRO<ZPRO OU NPRO>ZPRO ET NPRO<ZPRO+36 ALORS ALLER EN 19
14 SI NPRO>ZPRO-13 ET NPRO<ZPRO-4 ALORS ALLER EN 21
15 SI NPRO>ZPRO+3 OU NPRO<ZPRO-12 ALORS ALLER EN 23
17 AFFICHER 'NOUS ALLONS VERIFIER SI CETTE EXCELLENTE'
18 AFFICHER 'REPONSE EST OU NON L"EFFET DU HASARD.';ALLER EN 26
19 AFFICHER 'ASSEZ BONNE REPONSE QUI MERITE UN PETIT EFFORT SUPPLEMENTAIRE.'
20 SI NPRO>23 ALORS NPRO_NPRO-8;ALLER EN 26
21 AFFICHER 'CETTE REPONSE MONTRE QU"UN EFFORT EST NECESSAIRE.';ALLER EN 26
23 AFFICHER 'TES CONNAISSANCES (?) NECESSITENT'
24 AFFICHER 'UNE SOLIDE REVISION SYSTEMATIQUE.'
25 SI NPRO>25 ALORS NPRO_NPRO-20
26 AFFICHER[2/,'ENUMERE LES NOMS DES DIFFERENTES CATEGORIES DE PRONOMS QUE TU']
27 AFFICHER 'CONNAIS. QUAND TU AS FINI TAPES FIN.'
28 FAIRE 37 POUR I_1 JUSQUA 8
29 AFFICHER[/,5X,'PRONOMS '];LIRE CAT
30 CHARGER PROCA,1,'PRONO';X_0
31 CATE_SCH(PROCA,1,'/');SI CAT=CATE ALORS DEBUT NPRO_NPRO+1;ALLER EN 37 FIN
32 X_X+1;SI CAT='FIN' ALORS ALLER EN 38
33 SI X=9 ALORS AFFICHER 'CETTE CATEGORIE N"EXISTE QUE DANS VOTRE IMAGINATION.'
34 SI X<9 ALORS ALLER EN 36
35 AFFICHER[/];ALLER EN 37
36 PROCA_SCH(PROCA,LGR(CATE)+2,LGR(PROCA)-(LGR(CATE)+1));ALLER EN 31
37* DERNIERE LIGNE DE LA BOUCLE
38 CHAINE A;CHARGER A,1,'PRONO';AFFICHER 'CORRECTION: ',A;PAUSE
39 &AFTEX()
40 AFFICHER[2/,'TOUTES LES CATEGORIES SONT-ELLES REPRESENTEES DANS CE TEXTE ?']
41 LIRE QUIZ;&NEN2()
42 &CAP()
43 AFFICHER[/,5X,'ERREUR !!!',2/];&CAP()
45 CHARGER CORR,NE+NEN+2,NOM
47 CORR_SCH(CORR,3,LGR(CATE)-2)
48 AFFICHER[2/,'CORRECTION:']
50 AFFICHER[2/,U,3/,'VEUX-TU CONSULTER LES TABLEAUX DES DIFFERENTES']CORR
51 AFFICHER 'CATEGORIES DE PRONOMS ?';LIRE QUIZ
52 SI QUIZ='OUI' ALORS &TABC()
53 NPRO_NPRO/(32/20)
55 AFFICHER[5/,'...ET VOICI TA NOTE PROVISOIRE : ',U,'/20']NPRO
60 EXECUTER 'PRON2'
100 PROCEDURE &AFTEX();&NEN()
101 CHARGER E6,NE+1,NOM;TITRE_SCH(E6,1,'/');OTEUR_SCH(E6,LGR(TITRE)+2,'/')
102 C_40-LGR(TITRE)/2;AFFICHER[/,*X,U,/]C,TITRE
103 FAIRE 115 POUR I_1 JUSQUA NE
104 CHARGER TEX,I,NOM;SI I>1 ALORS TEX_TEXT!' '!TEX;&SUIT()
105 L_79;L0_LGR(TEX)
106 PL_SCH(TEX,1,L)
108 SI SCH(PL,L,1)=' ' ALORS ALLER EN 109 SINON L_L-1;ALLER EN 106
109 SI I<NE ET LGR(TEX)-L<80 ALORS DEBUT TEXT_TEX;ALLER EN 115 FIN
110 AFFICHER PL;TEX_SCH(TEX,L+1,LGR(TEX)-L)
111 SI I=NE ET LGR(TEX)<80 ALORS DEBUT AFFICHER TEX;ALLER EN 115 FIN
112 ALLER EN 105
115* DERNIERE LIGNE DE LA BOUCLE
116 AFFICHER[2/,60X,U]OTEUR
117 RETOUR
120 PROCEDURE &TABC()
123 AFFICHER[2/]
125 AFFICHER 'NOMME LE NOM DE LA CATEGORIE DE PRONOMS QUE TU VEUX CONSULTER'
126 AFFICHER[/,5X,'PRONOMS '];LIRE QUIZ
128 SI QUIZ='INDEFINIS' ALORS DEBUT CHARGER CAT,2,'PRONO';AFFICHER CAT FIN
129 SI QUIZ='RELATIFS' ALORS DEBUT CHARGER CAT,3,'PRONO';AFFICHER CAT FIN
130 SI QUIZ='DEMONSTRATIFS' ALORS DEBUT CHARGER CAT,4,'PRONO';AFFICHER CAT FIN
131 SI QUIZ#'DEMONSTRATIFS-RELATIFS' ALORS ALLER EN 133
132 CHARGER CAT,5,'PRONO';AFFICHER CAT;ALLER EN 134
133 SI QUIZ='DEMONSTRATIFS RELATIFS' ALORS ALLER EN 132
134 SI QUIZ='PERSONNELS' ALORS DEBUT CHARGER CAT,6,'PRONO';AFFICHER CAT FIN
135 SI QUIZ='REFLECHIS' ALORS DEBUT CHARGER CAT,7,'PRONO';AFFICHER CAT FIN
136 SI QUIZ='POSSESSIFS' ALORS DEBUT CHARGER CAT,8,'PRONO';AFFICHER CAT FIN
137 SI QUIZ='INTERROGATIFS' ALORS DEBUT CHARGER CAT,9,'PRONO';AFFICHER CAT FIN
138 AFFICHER[/,'VEUX-TU CONSULTER UN AUTRE TABLEAU ?'];LIRE QUIZ
140 SI QUIZ='OUI' ALORS ALLER EN 123 SINON RETOUR
150 PROCEDURE &NEN()
152 CHARGER E6,1,NOM
154 QUIZ_SCH(E6,1,'/')
156 NE_CNB(E6,1)
158 RETOUR
160 PROCEDURE &SUIT()
162 SI I=1 ALORS TEX_SCH(TEX,3,LGR(TEX)-2)
164 RETOUR
170 PROCEDURE &NBPRO()
172 CHARGER QUIZ,NE+2,NOM
174 PROCA_SCH(QUIZ,1,'/')
176 NEN_CNB(PROCA,1)
178 CAT_SCH(QUIZ,3,'/')
180 ZPRO_CNB(CAT,1);RETOUR
200 PROCEDURE &NEN2()
202 CHARGER CATE,NE+NEN+2,NOM
204 PROCA_SCH(CATE,1,'/')
206 NC_CNB(PROCA,1)
208 CATE_SCH(CATE,LGR(PROCA)+2,LGR(CATE)-(LGR(PROCA)-1))
210 RETOUR
220 PROCEDURE &CAP()
222 SI QUIZ='NON' ET NC=0 ALORS RETOUR EN 43
224 SI QUIZ='OUI' ET NC#0 ALORS AFFICHER[/,5x,'ERREUR !!!',2/]
226 SI NC=0 ALORS RETOUR EN 50
228 AFFICHER 'NOMME LES CATEGORIES NON REPRESENTEES:'
230 FAIRE 245 POUR I_1 JUSQUA NC
232 CHARGER CATE,NE+NEN+2,NOM
233 CATE_SCH(CATE,3,LGR(CATE)-2)
234 AFFICHER[/,5X,'PRONOMS '];LIRE QUIZ
236 PROCA_SCH(CATE,1,'/',BX)
238 SI PROCA=QUIZ ALORS DEBUT NPRO_NPRO+2;ALLER EN 245 FIN
240 SI BX=LGR(CATE)+1 ALORS DEBUT NPRO_NPRO-1;AFFICHER 'NON';ALLER EN 245 FIN
242 CATE_SCH(CATE,LGR(PROCA)+2,LGR(CATE)-LGR(PROCA)-1)
244 ALLER EN 236
245*FIN DE BOUCLE
247 RETOUR EN 45
